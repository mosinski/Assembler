<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
        "http://www.w3.org/TR/html4/loose.dtd">
<HTML lang="pl">
<HEAD>
<META HTTP-EQUIV="Content-Type"       CONTENT="text/html; charset=ISO-8859-2">
<META HTTP-EQUIV="Content-Language"   CONTENT="pl">
<META http-equiv="Content-Style-Type" content="text/css">
<META HTTP-EQUIV="X-Frame-Options"    CONTENT="DENY">
<LINK rel="stylesheet" href="../asm.css" type="text/css">

<TITLE> Asembler: Linuks, czê¶æ 8 - Zaawansowane programy </TITLE>
<link rel="Start"    hreflang="pl" lang="en" href="../index.htm" >
<link rel="Prev"     hreflang="pl" lang="en" href="linux07.html" >
<link rel="Next"     hreflang="pl" lang="en" href="linux09.html" >
<link rel="Contents" hreflang="pl" lang="en" href="../index.htm" >

<META NAME="Author" CONTENT="Bogdan D.">
<META NAME="Description" CONTENT="Kurs jêzyka asembler">
<META NAME="Keywords" CONTENT="linux, linuks, kurs, asm, asembler, assembler">
<META NAME="Language" CONTENT="pl">
<META NAME="Generator" CONTENT="KWrite">
<meta http-equiv="Last-Modified" content="Mon, Oct 29 2012 17:22:20 CEST">
</HEAD><BODY>

<div class="c">Jak pisaæ programy w jêzyku asembler pod Linuksem?<BR>
	<h1 class="nag">Czê¶æ 8 - Zaawansowane programy, czyli zobaczmy,
	 co ten jêzyk naprawdê potrafi</h1></div>

<P>
No có¿, nie jeste¶my ju¿ amatorami i przysz³a pora, aby przyjrzeæ siê temu, w czym asembler
wprost b³yszczy: algorytmy intensywne obliczeniowo. Specjalnie na potrzeby tego kursu
napisa³em nastêpuj±cy programik. Zaprezentujê w nim kilka sztuczek i poka¿ê, do jakich rozmiarów
(tutaj: 2 instrukcje) mo¿na ¶cisn±æ g³ówn± pêtlê programu.<BR>
Oto ten programik:</P>
	<BR><a href="#linux0801" tabindex="1" class="bezdruk">(przeskocz program obliczaj±cy sumê liczb)</a>
<PRE title="program obliczaj±cy sumê liczb od 1 do n">
; Program licz±cy sumê liczb od 1 do liczby wpisanej z klawiatury
;
; Autor: Bogdan D.
;
; kompilacja:
;
; nasm -O999 -f elf ciag_ar.asm
; ld -s -o ciag_ar ciag_ar.o bibl/lib/libasmio.a
;
; fasm ciag_ar.asm ciag_ar.o
; ld -s -o ciag_ar ciag_ar.o bibl/lib/libasmio.a

; FASM:
; format ELF
; include &quot;bibl/incl/linuxbsd/fasm/std_bibl.inc&quot;
; section &quot;.text&quot; executable
; public _start

; NASM
%include &quot;bibl/incl/linuxbsd/nasm/std_bibl.inc&quot;
section .text
global _start



_start:
	pisz
	db	&quot;Program liczy sume liczb od 1 do podanej liczby.&quot;,lf
	db	&quot;Podaj liczbe calkowita: &quot;,0

	we32e		; pobieramy z klawiatury liczbê do rejestru EAX
	jnc	liczba_ok	; flaga CF=1 oznacza b³±d


blad:
	pisz
	db	lf, &quot;Zla liczba!&quot;,lf,0

	wyjscie 1		; mov ebx, 1 / mov eax, 1 / int 80h

liczba_ok:
	test	eax, eax	; je¶li EAX=0, to te¿ b³±d
	jz	blad

	mov	ebx, eax	; zachowaj liczbê. EBX=n
	xor	edx, edx	; EDX = nasza suma
	mov	ecx, 1

petla:
	add	edx, eax	; dodaj liczbê do sumy
	sub	eax, ecx	; odejmij 1 od liczby
	jnz	petla		; liczba ró¿na od zera?
				; to jeszcze raz dodajemy

	pisz
	db	lf, &quot;Wynik z sumowania 1+2+3+...+n= &quot;,0

	mov	eax, edx	; EAX = wynik
	pisz32e			; wypisz EAX

	mov	eax, ebx	; przywrócenie liczby
	add	eax, 1		; EAX = n+1
	mul	ebx		; EDX:EAX = EAX*EBX = n*(n+1)

	shr	edx, 1
	rcr	eax, 1		; EDX:EAX = EDX:EAX/2

	pisz
	db	lf, &quot;Wynik ze wzoru: n(n+1)/2= &quot;,0

	pisz64		; wypisuje na ekranie 64-bitow± liczbê ca³kowit±
			; z EDX:EAX


	nwln
	wyjscie 0</PRE>

<P><a name="linux0801" id="linux0801">Jak widaæ, nie jest on ogromny</a>,
 a jednak spe³nia swoje zadanie. Teraz przeanalizujemy ten krótki programik:</P>
<UL>
 <LI>Komentarz nag³ówkowy.
  <P>Mówi, co program robi oraz kto jest jego autorem. Mo¿e zawieraæ informacje o wersji programu,
  o niestandardowym sposobie kompilacji/uruchomienia i wiele innych szczegó³ów.</P></LI>

 <LI>pisz, we32e, pisz32e oraz pisz64.
  <P>To s± makra uruchamiaj±ce procedury z mojej biblioteki. U¿ywam ich, bo s± sprawdzone i nie
  muszê ci±gle umieszczaæ kodu tych procedur w programie.</P></LI>

 <LI><P>Makro <q>wyjscie</q> zawiera w sobie kod wyj¶cia z programu, napisany obok.</P></LI>

 <LI><CODE>test rej, rej / jz ... / jnz ...</CODE>
   <P>Instrukcja <code>TEST</code> jest szybsza ni¿ <CODE>cmp rej, 0</CODE>
    i nie zmienia zawarto¶ci rejestru, w przeciwieñstwie do <code>OR</code>.
    Jest to najszybszy sposób na sprawdzenie, warto¶æ rejestru wynosi 0.
   </P></LI>

 <LI>Pêtla g³ówna.
   <P>Jak widaæ, najpierw do sumy dodajemy n, potem n-1, potem n-2, i na koñcu 1. Umo¿liwi³o to
   znaczne skrócenie kodu pêtli, a wiêc zwiêkszenie jej szybko¶ci. Napisanie <CODE>sub eax, ecx</CODE>
   zamiast <CODE>sub eax, 1</CODE> skraca rozmiar instrukcji i powoduje jej przyspieszenie,
   gdy¿ dziêki temu w samej pêtli procesor operuje ju¿ tylko na rejestrach.</P></LI>

 <LI><CODE>shr edx, 1 / rcr eax, 1</CODE>
   <P>Wynik musimy podzieliæ przez 2, zgodnie ze wzorem.
   Niestety, nie ma instrukcji <CODE>shr</CODE> dla 64 bitów. Wiêc trzeba ten brak jako¶ obej¶æ.
   Najpierw, <CODE>shr edx, 1</CODE> dzieli EDX przez 2, a bit 0 l±duje we fladze CF. Teraz,
   <CODE>rcr eax, 1</CODE> (<span lang="en">rotate THROUGH CARRY</span>) warto¶æ CF
    (czyli stary bit 0 EDX) umie¶ci w bicie 31 EAX. I o to chodzi³o!</P></LI>
</UL>

<hr>
<P>
Poni¿szy programik te¿ napisa³em dla tego kursu. Ma on pokazaæ z³o¿one sposoby adresowania oraz
instrukcje warunkowego przesuniêcia (<code>CMOV..</code>):</P>

	<BR><a href="#linux0802" class="bezdruk">(przeskocz program z macierz±)</a>

<PRE title="program znajduj±cy najwiêkszy i namniejszy element macierzy">
; Program wczytuje od u¿ytkownika macierz 3x3, po czym znajduje
; element najwiêkszy i najmniejszy
;
; Autor: Bogdan D.
;
; kompilacja:
; nasm -O999 -f elf macierze.asm
; ld -s -o macierze macierze.o -Lbibl/lib -lasmio
;
; fasm macierze.asm macierze.o
; ld -s -o macierze macierze.o -Lbibl/lib -lasmio

; FASM:
; format ELF
; include &quot;bibl/incl/linuxbsd/fasm/std_bibl.inc&quot;
; section &quot;.text&quot; executable
; public _start
; rozmiar = 3

; NASM
%include &quot;bibl/incl/linuxbsd/nasm/std_bibl.inc&quot;
%define rozmiar 3
section .text
global _start


_start:

	pisz
	db	&quot;Prosze podac wszystkie 9 elementow macierzy,&quot;
	db	lf, &quot;a ja znajde najwiekszy i najmniejszy.&quot;,lf,0

	xor	edx, edx			; ECX = 0
	mov	ebx, macierz

petla_wczyt:
	pisz
	db	&quot;Prosze podac element numer &quot;,0
	mov	eax, edx
	add	eax, 1
	pisz32e				; wypisz numer elementu

	push	ebx
	push	edx

	mov	eax, 4
	mov	ebx, 1
	mov	ecx, dwukspc
	mov	edx, 2
	int	80h			; wypisz dwukropek i spacjê

	pop	edx
	pop	ebx

	we32e				; wczytaj element
	jc	blad
	mov	[ebx+4*edx], eax	; umie¶æ w macierzy

	add	edx, 1			; zwiêksz licznik elementów
					; i równocze¶nie pozycjê w macierzy

	cmp	edx, rozmiar*rozmiar
	jb	petla_wczyt

	jmp	wczyt_gotowe

blad:
	pisz
	db	lf, &quot;Zla liczba!&quot;,lf,0

	wyjscie 1

wczyt_gotowe:
					; EBP = max, EDI = min

	mov	ebp, [ebx]
	mov	edi, [ebx]		; pierwszy element
	mov	edx, 1
	mov	eax, 1
	mov	esi, rozmiar*rozmiar

znajdz_max_min:
	mov	ecx, [ ebx + 4*edx ]
	cmp	ebp, ecx		; EBP &lt; macierz[edx] ?
	cmovb	ebp, ecx		; je¶li tak, to EBP = macierz[edx]

	cmp	edi, ecx		; EDI &gt; macierz[edx] ?
	cmova	edi, ecx		; je¶li tak, to EDI = macierz[edx]

	add	edx, eax
	cmp	edx, esi
	jb	znajdz_max_min

	pisz
	db	lf, &quot;Najwiekszy element: &quot;,0
	mov	eax, ebp
	pisz32e

	pisz
	db	lf, &quot;Najmniejszy element: &quot;,0
	mov	eax, edi
	pisz32e

	nwln
	wyjscie 0

; FASM: section &quot;.data&quot; writeable
section .data

macierz		times	rozmiar*rozmiar		dd 0
dwukspc		db &quot;: &quot;</PRE>



<P><a name="linux0802" id="linux0802">Przypatrzmy siê teraz miejscom</a>, gdzie mo¿na zw±tpiæ
w swoje umiejêtno¶ci:</P>
<UL>
 <LI><CODE>mov	[ebx+4*edx], eax</CODE>
  <P>EBX = adres macierzy. EDX = 0, 1, 2, ..., rozmiar*rozmiar=9. Elementy macierzy maj± rozmiar po
  4 bajty ka¿dy, st±d EDX mno¿ymy przez 4. Innymi s³owy, pierwszy EAX idzie do [ebx+4*0]=[ebx],
  drugi do [ebx+4] (na 2 miejsce macierzy), trzeci do [ebx+8] itd.</P></LI>

 <LI>Fragment kodu:<BR>
  <PRE>
	mov	ecx, [ ebx + 4*edx ]
	cmp	ebp, ecx	; EBP &lt; macierz[edx] ?
	cmovb	ebp, ecx	; je¶li tak, to EBP = macierz[edx]

	cmp	edi, ecx	; EDI &gt; macierz[edx] ?
	cmova	edi, ecx	; je¶li tak, to EDI = macierz[edx]

	add	edx, eax
	cmp	edx, esi
	jb	znajdz_max_min   </PRE>

  <P>Najpierw, do ECX idzie aktualny element. Potem porównujemy EBX z tym elementem i, gdy EBP &lt; ECX,
  kopiujemy ECX do EBP. Do tego w³a¶nie s³u¿y instrukcja <CODE>CMOVB</CODE>
   (<span lang="en">Conditional MOVe if Below</span>).
    Instrukcje z rodziny <code>(F)CMOV</code> umo¿liwiaj± pozbywanie siê skoków warunkowych,
  które obni¿aj± wydajno¶æ kodu.<BR>
  Podobnie, porównujemy EDI=min z ECX.<BR>
  Potem, zwiêkszamy EDX o 1 i sprawdzamy, czy nie przeszli¶my przez ka¿dy element macierzy.
  </P></LI>
</UL>

<P>Powy¿szy program trudno nazwaæ <q>intensywnym obliczeniowo</q>, bo ograniczy³em rozmiar macierzy do
3x3. Ale to by³ tylko przyk³ad. Prawdziwe programy mog± operowaæ na macierzach/tablicach
zawieraj±cych
miliony elementów. Podobny program napisany w HLLu jak C czy Pascal po prostu zaliczy³by siê na
¶mieræ.
</P>
<hr>
<P>
Teraz poka¿ê program, który ewoluowa³ od nieoptymalnej formy (zawieraj±cej na przyk³ad wiêcej skoków
warunkowych w g³ównej pêtli oraz inne nieoptymalne instrukcje) do czego¶ takiego:</P>
	<BR><a href="#linux0803" class="bezdruk">(przeskocz program znajduj±cy liczby magiczne)</a>
<PRE title="program znajduj±cy liczby magiczne">
; L_mag.asm
;
; Program wyszukuje liczby, które s± sum± swoich dzielników
;
; Autor: Bogdan D.
; kontakt: bogdandr (at) op (dot) pl
;
; nasm -O999 -f elf l_mag.asm
; ld -s -o l_mag l_mag.o
;
; fasm l_mag.asm l_mag

; FASM:
; format ELF executable
; entry _start
; segment readable executable
; lf = 10

; NASM
%define lf 10		; znak przej¶cia do nowej linii (Line Feed)
section .text
global _start


_start:
	mov	ebx,1		; liczba pocz±tkowa

	mov	ebp,1

align 16
start2:
	mov	esi,ebx		; ESI = liczba

	mov	ecx,ebp		; EBP = 1
	shr	esi,1		; zachowanie po³owy liczby

	xor	edi,edi		; suma dzielników=0

align 16
petla:
	xor	edx,edx		; dla dzielenia
	nop
	cmp	ecx,esi		; czy ECX (dzielnik)&gt;liczba/2?
	mov	eax,ebx		; przywrócenie liczby do dzielenia
	nop
	ja	dalej2		; Je¶li ECX &gt; ESI, to koniec
				; dzielenia tej liczby

	nop
	div	ecx		; EAX = EDX:EAX / ECX, EDX=reszta

	nop
	nop
	add	ecx,ebp		; zwiêkszamy dzielnik o 1
	nop

	test	edx,edx		; czy ECX jest dzielnikiem?
				; (czy EDX=reszta=0?)
	nop
	nop
	jnz	petla		; nie? - dzielimy przez nastêpn± liczbê

				; tak? -
	lea	edi,[edi+ecx-1]	; dodajemy dzielnik do sumy,
				; nie sprawdzamy na przepe³nienie.
				; ECX-1 bo dodali¶my EBP=1 do ECX po DIV.

	jmp	short petla	; dzielimy przez kolejn± liczbê
	ud2

align 16
dalej2:
	cmp	ebx,edi		; czy to ta liczba?
				; (czy liczba=suma dzielników?)
	jne	nie		; nie

	push	ebx

	mov	eax, 4
	mov	ebx, 1
	mov	ecx, jest
	mov	edx, jest_dlugosc
	int	80h		; tak - napis &quot;znaleziono&quot;

	pop	ebx

	mov	eax,ebx
	call	pl		; wypisujemy liczbê

align 16
nie:

	cmp	ebx,0ffffffffh	; czy ju¿ koniec zakresu?
	nop
	je	koniec		; tak

	add	ebx,ebp		; nie, zwiêkszamy liczbê badan± o 1
	nop
	jmp	start2		; i idziemy od pocz±tku
	ud2

align 16
koniec:

	push	ebx

	mov	eax, 4
	mov	ebx, 1
	mov	ecx, meta
	mov	edx, meta_dlugosc
	int	80h

	pop	eax
	call	pl		; wypisujemy ostatni± sprawdzon± liczbê

        mov     eax, 4
        mov     ebx, 1
        mov     ecx, nwln
        mov     edx, 1
        int     80h		; wypisz znak nowej linii

	mov	eax, 1
	xor	ebx, ebx
	int	80h

	ud2

align 16
pc:				; wypisuje cyfrê w AL
	push	eax
	or	al, &quot;0&quot;
	mov	[cyfra], al

	push	ebx
	push	ecx
	push	edx

	mov	eax, 4
	mov	ebx, 1
	mov	ecx, cyfra
	mov	edx, 1
	int	80h

	pop	edx
	pop	ecx
	pop	ebx
	pop	eax

	ret
	ud2

align 16
pl:			; wy¶wietla liczbê dziesiêciocyfrow± w EAX
	mov	ecx,1000000000
	xor	edx,edx
	div	ecx
	call	pc

	mov	eax,edx
	mov	ecx,100000000
	xor	edx,edx
	div	ecx
	call	pc

	mov	eax,edx
	mov	ecx,10000000
	xor	edx,edx
	div	ecx
	call	pc

	mov	eax,edx
	mov	ecx,1000000
	xor	edx,edx
	div	ecx
	call	pc

	mov	eax,edx
	mov	ecx,100000
	xor	edx,edx
	div	ecx
	call	pc

	mov	eax,edx
	mov	ecx,10000
	xor	edx,edx
	div	ecx
	call	pc

	mov	eax,edx
	xor	edx,edx
	mov	ecx,1000
	div	ecx
	call	pc

	mov	eax,edx
	mov	cl,100
	div	cl
	mov	ch,ah
	call	pc

	mov	al,ch
	xor	ah,ah
	mov	cl,10
	div	cl
	mov	ch,ah
	call	pc

	mov	al,ch
	call	pc
	ret
	ud2


; FASM:  segment readable writeable
section .data align=4

jest	db	lf,&quot;Znaleziono: &quot;
jest_dlugosc	equ	$-jest	; FASM: "=" zamiast "equ"

meta	db	lf,&quot;Koniec. ostatnia liczba: &quot;
meta_dlugosc	equ	$-meta	; FASM: "=" zamiast "equ"

cyfra	db	0
nwln	db	lf</PRE>

<a name="linux0803" id="linux0803">A oto analiza</a>:
<UL>
 <LI>Pêtla g³ówna:
	<P>Dziel EBX przez kolejne przypuszczalne dzielniki. Je¶li trafisz na prawdziwy dzielnik
	(reszta=EDX=0), to dodaj go do sumy=EDI.<BR>
	Unika³em ustawiania obok siebie takich instrukcji, które zale¿± od siebie, jak na przyk³ad
	<CODE>CMP / JA</CODE> czy <CODE>DIV / ADD</CODE>
	</P></LI>

 <LI>Nie za du¿o tych <CODE>NOP</CODE>ów?
	<P>Nie. Zamiast czekaæ na wynik poprzednich instrukcji, procesor zajmuje siê... robieniem
	niczego. Ale jednak siê zajmuje. Wspó³czesne procesory potrafi± wykonywaæ wiele
	niezale¿nych instrukcji praktycznie równolegle. Wiêc w czasie, jak procesor czeka na
	wykonanie poprzednich instrukcji, mo¿e równolegle wykonywaæ NOPy. Zwiêksza to
	przepustowo¶æ, utrzymuje uk³ady dekoduj±ce w ci±g³ej pracy, kolejka instrukcji
	oczekuj±cych na wykonanie nie jest pusta.
	</P></LI>

 <LI>Co robi instrukcja <CODE>lea edi,[edi+ecx-1]</CODE> ?
	<P><code>LEA</code> -<span lang="en"> Load Effective Address</span>. Do rejestru EDI
	za³aduj ADRES (elementu, którego) ADRES
	wynosi EDI+ECX-1. Czyli, w paskalowej sk³adni: EDI := EDI+ECX-1. Do EDI dodajemy
	znaleziony dzielnik. Musimy odj±æ 1, bo wcze¶niej (po dzieleniu) zwiêkszyli¶my ECX o 1.
	</P></LI>

 <LI>Co robi instrukcja <CODE>UD2</CODE> i czemu jest umieszczona po instrukcjach JMP ?
	<P>Ta instrukcja (<span lang="en">UnDefined opcode</span> 2) wywo³uje wyj±tek wykonania nieprawid³owej
	instrukcji przez procesor. Umie¶ci³em j± w takich miejscach, ¿eby nigdy nie by³a
	wykonana.<BR>
	Po co ona w ogóle jest w tym programie w takich miejscach?<BR>
	Ma ona interesuj±c± w³a¶ciwo¶æ: powstrzymuje jednostki dekoduj±ce instrukcje od dalszej
	pracy. Po co dekodowaæ instrukcje, które i tak nie bêd± wykonane (bo by³y po skoku
	bezwarunkowym)? Strata czasu.
	</P></LI>

 <LI>Po co ci±gle <CODE><span lang="en">align</span> 16</CODE> ?
	<P>Te dyrektywy s± tylko przed etykietami, które s± celem skoku. Ustawianie kodu od
	adresu, który dzieli siê przez 16 mo¿e u³atwiæ procesorowi umieszczenie go w ca³ej jednej
	linii pamiêci podrêcznej (cache). Mniej instrukcji musi byæ pobieranych z pamiêci
	(bo te, które s± najczê¶ciej wykonywane ju¿ s± w cache), wiêc szybko¶æ dekodowania
	wzrasta. Uk³adania kodu i danych zwiêksza ogóln± wydajno¶æ programu
	</P></LI>

</UL>
<P>O tych wszystkich sztuczkach, które tu zastosowa³em, mo¿na przeczytaæ w podrêcznikach dotycz±cych
optymalizacji programów, wydanych zarówno przez Intel, jak i AMD (u AMD s± te¿ wymienione
sztuczki, których mo¿na u¿yæ do optymalizacji programów napisanych w jêzyku C).<BR>
Podajê adresy (te same co zwykle):
<A class="amd_link" href="http://developer.amd.com/Resources/documentation/guides/Pages/default.aspx" hreflang="en"><acronym title="Advanced Micro Devices" lang="en">AMD</acronym></A>,
<A class="intel_link" href="http://www.intel.com/content/www/us/en/processors/architectures-software-developer-manuals.html" hreflang="en">Intel</A>.</P>
<P>
¯yczê ciekawej lektury i mi³ej zabawy.</P>

<br><br>
<div class="bezdruk">
<a accesskey="3" hreflang="pl" href="linux07.html">Poprzednia czê¶æ kursu</a> (Alt+3)<br>
<a accesskey="4" hreflang="pl" href="linux09.html">Kolejna czê¶æ kursu</a> (Alt+4)<br>
<A accesskey="1" hreflang="pl" href="../index.htm">Spis tre¶ci off-<span lang="en">line</span></a> (Alt+1)<BR>
<A accesskey="2" hreflang="pl" href="../index.php">Spis tre¶ci on-<span lang="en">line</span></a> (Alt+2)<br>
<A accesskey="0" hreflang="pl" href="../ulatwie.htm">U³atwienia dla niepe³nosprawnych</a> (Alt+0)<BR>
</div>


<BR><BR><HR>
<h2 class="nag">Æwiczenia</h2>
<OL>
 <LI>Napisz program obliczaj±cy Najwiêkszy Wspólny Dzielnik i Najmniejsz± Wspóln± Wielokrotno¶æ
	dwóch liczb wiedz±c, ¿e:<BR>
	NWD(a,b) = NWD(b, reszta z dzielenia a przez b) i NWD(n,0)=n (algorytm Euklidesa)<BR>
	NWW(a,b) = a*b / NWD(a,b)
	<BR><BR></LI>
 <LI>Napisz program rozk³adaj±cy dan± liczbê na czynniki pierwsze (liczba mo¿e byæ umieszczona
	w kodzie programu).</LI>
</OL>

</BODY></HTML>
