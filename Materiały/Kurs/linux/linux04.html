<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN"
        "http://www.w3.org/TR/html4/loose.dtd">
<HTML lang="pl">
<HEAD>
<META HTTP-EQUIV="Content-Type"       CONTENT="text/html; charset=ISO-8859-2">
<META HTTP-EQUIV="Content-Language"   CONTENT="pl">
<META http-equiv="Content-Style-Type" content="text/css">
<META HTTP-EQUIV="X-Frame-Options"    CONTENT="DENY">
<LINK rel="stylesheet" href="../asm.css" type="text/css">

<TITLE> Asembler: Linuks, czê¶æ 4 - Pierwsze programy </TITLE>
<link rel="Start"    hreflang="pl" lang="en" href="../index.htm" >
<link rel="Prev"     hreflang="pl" lang="en" href="linux03.html" >
<link rel="Next"     hreflang="pl" lang="en" href="linux05.html" >
<link rel="Contents" hreflang="pl" lang="en" href="../index.htm" >


<META NAME="Author" CONTENT="Bogdan D.">
<META NAME="Description" CONTENT="Kurs jêzyka asembler">
<META NAME="Keywords" CONTENT="kurs, asm, asembler, assembler, flagi, _pisz_ch, ile_pier, linux, linuks">
<META NAME="Language" CONTENT="pl">
<META NAME="Generator" CONTENT="Kwrite">
<meta http-equiv="Last-Modified" content="Tue, Jan 18 2011 17:59:57 CEST">
</HEAD><BODY>

<div class="c">Jak pisaæ programy w jêzyku asembler pod Linuksem?<BR>
	<h1 class="nag">Czê¶æ 4 - Pierwsze programy, czyli prze³amywanie pierwszych lodów</h1></div>

<P>
Znamy ju¿ rejestry, trochê instrukcji i zasad. No ale teoria jest niczym bez praktyki. Dlatego
w tej czê¶ci przedstawiê kilka wzglêdnie prostych programów, które powinny rozbudziæ wyobra¼niê
tworzenia.
</P>
<P>
Ten program spyta siê u¿ytkownika o imiê i przywita siê z nim:</P>
	<BR><a href="#linux0401_1" tabindex="1" class="bezdruk">(przeskocz program pytaj±cy o imiê)</a>
<PRE>
	; Program witaj±cy siê z u¿ytkownikiem po imieniu
	;
	; Autor: Bogdan D.
	; kontakt: bogdandr (at) op (dot) pl
	;
	; kompilacja:
	; nasm -f elf czesc.asm
	; ld -s -o czesc czesc.o
	;
	; kompilacja FASM:
	; fasm czesc.asm czesc

	; dla FASMa:
	;format ELF executable
	;entry _start

	;segment readable executable		; pocz±tek sekcji kodu

	; dla NASMa:
	section .text		; pocz±tek sekcji kodu
	global _start	; _start bêdzie symbolem globalnym,
			; od którego zacznie siê wykonywanie programu

	_start:
		mov	eax, 4		; zapis do pliku
		mov	ebx, 1		; na ekran
		mov	ecx, jak_masz	; napis do wy¶wietlenia: pytanie
		mov	edx, jak_masz_dl	; d³ugo¶æ napisu
		int	80h		; wy¶wietlamy

		mov	eax, 3		; czytanie z pliku
		mov	ebx, 0		; z klawiatury
		mov	ecx, imie	; dok±d czytaæ?
		mov	edx, imie_dl	; ile bajtów czytaæ?
		int	80h		; wczytujemy


		mov	eax, 4		; zapis do pliku
		mov	ebx, 1		; na ekran
		mov	ecx, czesc	; napis do wy¶wietlenia: &quot;cze¶æ&quot;
		mov	edx, czesc_dl	; d³ugo¶æ napisu
		int	80h		; wy¶wietlamy

		mov	eax, 4		; zapis do pliku
		mov	ebx, 1		; na ekran
		mov	ecx, imie	; napis do wy¶wietlenia: imiê
		mov	edx, imie_dl	; d³ugo¶æ napisu
		int	80h		; wy¶wietlamy

		mov	eax, 1
		xor	ebx, ebx
		int	80h

	; dla FASMa:
	;segment readable writeable		; pocz±tek sekcji danych

	section .data		; pocz±tek sekcji danych

	jak_masz	db	&quot;Jak masz na imie? &quot;
	; FASM: znak równo¶ci zamiast EQU
	jak_masz_dl	equ	$ - jak_masz

	; rezerwuj 20 bajtów o warto¶ci pocz±tkowej zero, na imiê
	imie:		times 20 db 0
	; FASM: znak równo¶ci zamiast EQU
	imie_dl		equ	$ - imie

	czesc		db	&quot;Czesc &quot;
	; FASM: znak równo¶ci zamiast EQU
	czesc_dl	equ	$ - czesc</pre>




<hr>
<P>
<a name="linux0401_1" id="linux0401_1">Nastêpny program</a> wypisuje na ekranie rejestr flag w postaci dwójkowej.
</P>
	<BR><a href="#linux0401" class="bezdruk">(przeskocz program wypisuj±cy flagi)</a>
<PRE title="Program wypisuj±cy flagi">
; Program wypisuj±cy flagi w postaci dwójkowej
;
; Autor: Bogdan D.
; kontakt: bogdandr (at) op (dot) pl
;
; kompilacja:
; nasm -f elf flagi.asm
; ld -s -o flagi flagi.o
;
; kompilacja FASM:
; fasm flagi.asm flagi


;format ELF executable		; dla FASMa
; entry _start			; dla FASMa

; segment readable executable	; dla FASMa

section .text		; tu zaczyna siê segment kodu,
			; nie jest to potrzebne

global _start		; nazwa punktu rozpoczêcia programu.
			; FASM: usun±æ tê linijkê

;CPU 386		; bêdziemy tu u¿ywaæ rejestrów 32-bitowych.
			; Nie jest to potrzebne, gdy¿
			; NASM domy¶lnie w³±cza wszystkie
			; mo¿liwe instrukcje.

_start:				; etykieta pocz±tku programu

	pushfd			; 32 bity flag id± na stos

	pop	esi		; flagi ze stosu do ESI

	mov	eax, &quot;0&quot;
	mov	ebx, nasze_flagi; EBX = adres bufora dla warto¶ci flag
	xor	edi, edi	; EDI = 0

	mov	cx, 32		; tyle bitów i tyle razy trzeba
				; przej¶æ przez pêtlê

petla:				; etykieta oznaczaj±ca pocz±tek pêtli.

	and	al, &quot;0&quot;	; upewniamy siê, ¿e AL zawiera tylko
				; 30h=&quot;0&quot;, co zaraz siê
				; mo¿e zmieniæ. Dok³adniej,
				; czy¶cimy bity 0-3,
				; z których bit 0 mo¿e siê zaraz zmieniæ

	shl	esi, 1		; Przesuwamy bity w ESI o 1 w lewo.
				; 31 bit ESI idzie do CF

	adc	al, 0		; ADC - add with carry. Do AL dodaj
				; 0 + warto¶æ CF.
				; je¶li CF (czyli 31 bit ESI) = 1,
				; to AL := AL+1,
				; inaczej AL bez zmian

	mov	[ebx+edi], al	; zapisz AL w buforze
	add	edi, 1

	loop	petla		; przejd¼ na pocz±tek pêtli,
				; je¶li nie skoñczyli¶my

	mov	eax, 4		; funkcja zapisywania do pliku/na ekran
	mov	ebx, 1		; 1 = ekran
	mov	ecx, nasze_flagi
	mov	edx, 32		; d³ugo¶æ tekstu
	int	80h		; wypisz na ekran

        mov     byte [nasze_flagi],0ah
        mov     eax, 4          ; funkcja zapisywania do pliku/na ekran
        mov     ebx, 1          ; 1 = ekran
        mov     ecx, nasze_flagi
        mov     edx, 1          ; d³ugo¶æ tekstu
        int     80h             ; wypisz na ekran przej¶cie do nowej linii

	mov	eax, 1
	int	80h		; wyj¶cie z programu

; FASM: segment readable writeable
section .data			; dane ju¿ nie mog± byæ w sekcji kodu, gdy¿
				; w Linuksie sekcja kodu programu jest
				; chroniona przed zapisem

nasze_flagi:	times	32	db &quot;0&quot;		; &quot;0&quot; = 30h</PRE>

<P>Kompilujemy go nastêpuj±co (wszystkie programy bêdziemy tak kompilowaæ, chyba ¿e powiem inaczej):</P>
<PRE>
	nasm -f elf flagi.asm
	ld -s -o flagi flagi.o</PRE>

<P>lub:</P>
<PRE>	fasm flagi.asm flagi</PRE>

<P><a name="linux0401" id="linux0401">Nie ma w tym programie wielkiej filozofii</a>.
 Nie powinno byæ trudno go zrozumieæ.</P>

<hr>
<P>Teraz krótki programik, którego jedynym celem jest wy¶wietlenie na ekranie cyfr od 0 do 9,
ka¿da w osobnej linii:</P>
	<BR><a href="#linux0402" class="bezdruk">(przeskocz program wypisuj±cy cyfry)</a>
<PRE title="program wypisuj±cy cyfry">
; Program wypisuje na ekranie cyfry od 0 do 9
;
; kompilacja NASM:
; nasm -f elf cyfry.asm
; ld -s -o cyfry cyfry.o


section .text

global _start

; definiujemy sta³e (NASM):

%define		lf	10		; Line Feed
%define	stdout	1	; standardowe urz±dzenie wyj¶cia (zwykle ekran)
%define		sys_write 4		; funkcja pisania do pliku

;
; kompilacja FASM:
; fasm cyfry.asm cyfry
;
; format ELF executable		; dla FASMa
; entry _start
; segment readable executable

; definiujemy sta³e (FASM):
; lf = 10
; stdout = 1
; sys_write = 4



_start:

	mov	eax, 0			; pierwsza wypisywana cyfra

wyswietlaj:
	call	_pisz_ld		; uruchom procedurê wy¶wietlania
					; liczby bêd±cej w EAX
	call	_nwln			; uruchom procedurê, która
					; przechodzi do nowej linii
	add	eax, 1			; zwiêkszamy cyfrê
	cmp	eax, 10			; sprawdzamy, czy ci±gle EAX &lt; 10
	jb	wyswietlaj		; je¶li EAX &lt; 10, to
					; wy¶wietlamy cyfrê

	mov	eax, 1			; funkcja wyj¶cia z programu
	xor	ebx, ebx		; kod wyj¶cia = 0
	int	80h			; wychodzimy



; nastêpuj±ce procedury (wy¶wietlanie liczby i przechodzenie
; do nowego wiersza) nie s± a¿ tak istotne, aby omawiaæ je
; szczegó³owo, gdy¿ w przysz³o¶ci bêdziemy u¿ywaæ tych samych
; procedur, ale z biblioteki, a te wstawi³em tutaj dla
; uproszczenia kompilacji programu.

; Ogólny schemat dzia³ania tej procedury wygl±da tak:
; we¼my liczbê EAX=12345. Robimy tak:
; 1. dzielimy EAX przez 10. reszta = EDX = DL = 5.
; Zapisz do bufora. EAX = 1234 (iloraz)
; 2. dzielimy EAX przez 10. reszta = DL = 4.
; Zapisz do bufora. EAX=123 (iloraz)
; 3. dzielimy EAX przez 10. reszta = DL = 3.
; Zapisz do bufora. EAX=12
; 4. dziel EAX przez 10. DL = 2. zapisz. iloraz = EAX = 1
; 5. dziel EAX przez 10. DL = 1. zapisz. iloraz = EAX = 0.
; Przerywamy pêtlê.
; Teraz w buforze s± znaki: &quot;54321&quot;. Wystarczy wypisaæ wspak
; i oryginalna liczba pojawia siê na ekranie.

_pisz_ld:

; pisz32e
; we: EAX=liczba bez znaku do wypisania

	pushfd
	push	ecx
	push	edx
	push	eax
	push	esi

	xor	esi, esi
	mov	ecx, 10

._pisz_ld_petla:
	xor	edx, edx
	div	ecx

	or	dl, &quot;0&quot;
	mov	[_pisz_bufor+esi], dl	; do bufora id± reszty z
					; dzielenia przez 10,
	inc	esi			; czyli cyfry wspak

	test	eax, eax
	jnz	._pisz_ld_petla

._pisz_ld_wypis:
	mov	al, [_pisz_bufor+esi-1]	; wypisujemy reszty wspak
	call	_pisz_z

	dec	esi
	jnz	._pisz_ld_wypis

	pop	esi
	pop	eax
	pop	edx
	pop	ecx
	popfd

	ret


_pisz_z:

; pisz_z
; we: AL=znak do wypisania

	push	eax
	push	ebx
	push	ecx
	push	edx

	mov	[_pisz_bufor+39], al

	mov	eax, sys_write		; funkcja zapisu do pliku
	mov	ebx, stdout		; kierujemy na
					; standardowe wyj¶cie
	lea	ecx, [_pisz_bufor+39]
	mov	edx, 1
	int	80h

	pop	edx
	pop	ecx
	pop	ebx
	pop	eax

	ret


_nwln:

;wy¶wietla znak koñca linii (Linux)

	push	eax

	mov	al, lf
	call	_pisz_z

	pop	eax
	ret


section .data
; FASM: segment readable writeable

_pisz_bufor:	times	40	db 0	; miejsce na 40 cyferek</PRE>

<P>
<a name="linux0402" id="linux0402">Nastêpny <q>twór</q> nie jest</a>
 wolnostoj±cym programem, ale pewn± procedur±. Pobiera ona informacje
z rejestru AL i wypisuje, co trzeba. Oto ona:</P>
	<BR><a href="#linux0403" class="bezdruk">(przeskocz procedurê _pisz_ch)</a>
<PRE title="procedura _pisz_ch">
; FASM: segment readable executable
section .text

_pisz_ch:

;we: AL=cyfra heksadecymalna do wypisania 0...15
; CF=1 je¶li b³±d

	push eax		; zachowaj modyfikowane rejestry: AX, Flagi
	pushfd

	cmp al,9		; Sprawdzamy dane wej¶ciowe :
				; AL jest w 0-9 czy w 10-15?
	ja _ch_hex		; AL &lt; 9. Skok do &quot;_ch_hex&quot;
	or al,&quot;0&quot;		; 0 &lt; AL &lt; 9. Or ustawia 2 bity,
				; czyni±c z AL liczbê z
				; przedzia³u 30h - 39h, czyli od &quot;0&quot; do &quot;9&quot;.
				; Mo¿na by³o napisaæ
				; &quot;add al,30h&quot;, ale zdecydowa³em siê
				; na &quot;or&quot;, bo jest
				; szybsze a efekt ten sam.

	jmp short _ch_pz	; AL ju¿ poprawione. Skacz do miejsca,
				; gdzie wypisujemy znak.

_ch_hex:			; AL &gt; 9. Mo¿e bêdzie to cyfra hex,
				; mo¿e nie.
	cmp al,15		; AL &gt; 15?
	ja _blad_ch		; je¶li tak, to mamy b³±d
	add al,&quot;A&quot;-10		; Du¿y skok my¶lowy. Ale wystarczy to
				; rozbiæ na 2 kroki i
				; wszystko staje siê jasne. Najpierw
				; odejmujemy 10 od AL.
				; Zamiast liczby od 10 do 15 mamy ju¿
				; liczbê od 0 do 5. Teraz tê liczbê
				; dodajemy do &quot;A&quot;, czyli kodu ASCII litery
				; A, otrzymuj±c znak od &quot;A&quot; do &quot;F&quot;

_ch_pz:				; miejsce wypisywania znaków.
	mov	[znak], al

	mov	eax, 4		; funkcja wypisywania
	mov	ebx, 1		; ekran
	mov	ecx, znak
	mov	edx, 1
	int	80h


	popfd			; zdejmij ze stosu flagi
	clc			; CF := 0 dla zaznaczenia braku b³êdu
				; (patrz opis procedury)
	jmp short _ch_ok	; skok do wyj¶cia

_blad_ch:			; sekcja obs³ugi b³êdu (AL &gt; 15)
	popfd			; zdejmij ze stosu flagi
	stc			; CF := 1 na znak b³êdu

_ch_ok:				; miejsce wyj¶cia z procedury
	pop eax			; zdejmij modyfikowane rejestry

	ret			; return, powrót

; FASM: segment readable writeable
section .data

znak	db	0</PRE>

<P><a name="linux0403" id="linux0403">To chyba nie by³o zbyt trudne</a>, co?<BR>
Szczegó³y dotycz±ce pisania procedur (i bibliotek) znajduj± siê w moim
<a href="bibl_tut_linux.html" hreflang="pl">artykule o pisaniu bibliotek</a>.</P>

<P>
Teraz poka¿ê pewien program, który wybra³em ze wzglêdu na du¿± liczbê ró¿nych instrukcji i
sztuczek. Niestety, nie jest on krótki, ale wspólnie spróbujemy przez niego przej¶æ.</P>
	<BR><a href="#linux0404" class="bezdruk">(przeskocz program zliczaj±cy liczby pierwsze)</a>
<PRE title="Program liczy liczby pierwsze">
; Program liczy liczby pierwsze w przedzia³ach
;   2-10, 2-100, 2-1000,... 2-100.000
;
; Autor: Bogdan D.
; kontakt: bogdandr (at) op (dot) pl
;
; kompilacja:
;
; nasm -f elf ile_pier.asm
; ld -s -o ile_pier ile_pier.o
;
; kompilacja FASM:
; fasm ile_pier.asm ile_pier


; format ELF executable		; tylko dla FASMa
; entry _start

; FASM: segment readable executable
section .text

global _start			; FASM: usun±æ

_start:				; pocz±tek...

	xor ebx,ebx		; EBX = liczba, któr± sprawdzamy,
				; czy jest pierwsza. Zaczniemy od 3.
				; Poni¿ej jest 3 razy &quot;inc&quot;
				; (zwiêksz o 1). Najpierw
				; EBX = 0, bo &quot;xor rej,rej&quot; zeruje dany
				; rejestr

	xor edi,edi		; EDI = bie¿±cy licznik liczb pierwszych

	xor ecx,ecx		; ECX = stary licznik liczb
				; (z poprzedniego przedzia³u)
				; Chwilowo, oczywi¶cie 0.

	inc ebx			; EBX = 1
	mov esi,10		; ESI = bie¿±cy koniec przedzia³u.
	inc edi			; EDI=1. uwzglêdniamy dwójkê, która
				; jest liczb± pierwsza
	inc ebx			; EBX=2, pierwsza liczba bêdzie = 3

petla:				; pêtla przedzia³u

	cmp ebx,esi		; czy koniec przedzia³u?
				; (ebx=liczba, esi=koniec przedzia³u)
	jae pisz		; EBX &gt;= ESI - id¼ do sekcji
				; wypisywania wyników

	mov ebp,2		; EBP - liczby, przez które
				; bêdziemy dzieliæ.
				; pierwszy dzielnik = 2

	inc ebx			; zwiêkszamy liczbê. EBX=3. Bêdzie
				; to pierwsza sprawdzana

spr:				; pêtla sprawdzania pojedynczej liczby

	mov eax,ebx		; EAX = sprawdzana liczba
	xor edx,edx		; EDX = 0
	div ebp			; EAX = EAX/EBP (EDX by³o=0),
				; EDX=reszta z dzielenia

	or edx,edx		; instrukcja OR tak jak wiele innych,
				; ustawi flagê zera ZF na 1, gdy jej
				; wynik by³ zerem. W tym przypadku
				; pytamy: czy EDX jest zerem?

	jz petla		; je¿eli dzieli siê bez reszty
				; (reszta=EDX=0),
				; to nie jest liczb± pierwsz±
				; i nale¿y zwiêkszyæ liczbê
				; sprawdzan± (inc ebx)

	inc ebp			; zwiêkszamy dzielnik

	cmp ebp,ebx		; dzielniki a¿ do liczby
	jb spr			; liczba &gt; dzielnik - sprawdzaj
				; dalej tê liczbê. Wiem, ¿e
				; mo¿na by³o sprawdzaæ tylko do
				; SQRT(liczba) lub LICZBA/2, ale
				; wyd³u¿y³oby to program i brakowa³o
				; mi ju¿ rejestrów...

juz:				; przerobili¶my wszystkie dzielniki,
				; zawsze wychodzi³a reszta
				; wiêc liczba badana jest pierwsza

	inc edi			; zwiêkszamy licznik liczb znalezionych
	jmp petla		; sprawdzaj kolejn± liczbê a¿
				; do koñca przedzia³u

				; sekcja wypisywania informacji

pisz:

	push ebx		; zachowujemy modyfikowane
				; a wa¿ne rejestry
	push ecx

	mov	eax, 4
	mov	ebx, 1
	mov	ecx, przedzial
	mov	edx, dlugosc_przedzial
	int	80h		; wypisujemy informacjê o przedziale

	mov	eax,esi		; EAX=ESI=koniec przedzia³u
	call	_pisz_ld	; wypisz ten koniec (EAX)

	mov	eax, 4
	mov	ebx, 1
	mov	ecx, dwuk
	mov	edx, 1
	int	80h		; wypisujemy dwukropek

	pop ecx

	add ecx,edi		; dodajemy poprzedni± liczbê
				; znalezionych liczb pierwszych
	mov eax,ecx		; EAX = liczba liczb pierwszych
				; od 2 do koñca bie¿±cego przedzia³u

	call _pisz_ld		; wypisujemy tê liczbê.

	pop ebx

	cmp esi,100000		; 10^5
	jb dalej		; ESI &gt; 100.000? Tak - koniec,
				; bo dalej liczy zbyt d³ugo
koniec:
	mov	eax, 4
	mov	ebx, 1
	mov	ecx, przedzial
	mov	edx, 1
	int	80h		; wypisujemy znak nowej linii


	xor	ebx, ebx	; kod wyj¶cia = 0
	mov	eax, 1
	int	80h		; wyj¶cie z programu

dalej:

	mov eax,esi		; EAX=ESI
	shl eax,3		; EAX = EAX*8
	shl esi,1		; ESI=ESI*2
	add esi,eax		; ESI=ESI*2+EAX*8=ESI*2+ESI*8=ESI*10.
				; Znacznie szybciej ni¿ MUL

	xor edi,edi		; bie¿±cy licznik liczb

	jmp petla		; robimy od pocz±tku...

_pisz_ld:

;we: EAX=liczba bez znaku do wypisania

	push ebx
	push ecx		; zachowujemy modyfikowane rejestry
	push edx
	push eax
	push esi

	xor esi,esi		; SI=0. Bêdzie wska¼nikiem w
				; powy¿szy bufor.

	mov ecx,10		; bêdziemy dzieliæ przez 10,
				; aby uzyskiwaæ kolejne cyfry
				; Reszty z dzielenia pójd± do
				; bufora, potem bêd± wypisane
				; wspak, bo pierwsza reszta
				; jest przecie¿ cyfr± jedno¶ci
_pisz_ld_petla:
	xor edx,edx		; EDX=0

	div ecx			; EAX = EAX/ECX, EDX = reszta,
				; która mie¶ci siê w DL, bo to
				; jest tylko 1 cyfra dziesiêtna.

	or  dl, &quot;0&quot;

	mov [_pisz_bufor+esi],dl	; Cyfra do bufora.

	inc esi			; Zwiêksz numer komórki w buforze,
				; do której bêdziemy teraz pisaæ

	or eax,eax		; EAX = 0 ?

	jnz _pisz_ld_petla	; Je¶li nie (JNZ), to skok do
				; pocz±tku pêtli

_pisz_ld_wypis:

	mov	eax, 4
	mov	ebx, 1
	lea	ecx, [_pisz_bufor+esi-1]
	mov	edx, 1
	int	80h

	dec esi			; zmniejsz wska¼nik do bufora.

	jnz _pisz_ld_wypis	; Je¶li ten wska¼nik (ESI) nie
				; jest zerem, wypisuj dalej

	pop esi			; odzyskaj zachowane rejestry
	pop eax
	pop edx
	pop ecx
	pop ebx

	ret                     ; powrót z procedury

; FASM: segment readable writeable
 section .data

 _pisz_bufor: times 20 db 0       ; miejsce na cyfry dla procedury

 przedzial       db      10,&quot;Przedzial 2-&quot;

; FASM:  dlugosc_przedzial       =     $ - przedzial
 dlugosc_przedzial       equ     $ - przedzial

 dwuk            db      &quot;:&quot;</PRE>
<hr>

<P><a name="linux0404" id="linux0404">Kilka uwag o tym programie</a>:</P>
<UL>
 <LI>Czemu nie zrobi³em <CODE>mov ebx,2</CODE> a potem <CODE>inc ebx</CODE>,
	 które musia³o byæ w pêtli?<BR>
	<P>Bo xor ebx,ebx jest krótsze i szybsze.</P></LI>

 <LI>Dobra. Wiêc czemu nie:
	<PRE>
	xor ebx,ebx
	inc ebx
	inc ebx	</PRE>
	<P>Te instrukcje operuj± na tym samym rejestrze i ka¿da musi poczekaæ, a¿ poprzednia siê
	zakoñczy. Wspó³czesne procesory potrafi± wykonywaæ niezale¿ne czynno¶ci równolegle,
	dlatego wcisn±³em w ¶rodek jeszcze kilka niezale¿nych instrukcji.</P></LI>

 <LI>Ten program sprawdza za du¿o dzielników. Nie mo¿na by³o sprawiæ, by sprawdza³ tylko do na przyk³ad
	po³owy sprawdzanej liczby?<BR>
	<P>Mo¿na by³o. U¿ywaj±c zmiennych w pamiêci. Niechêtnie to robiê, bo w porównaniu z
	prêdko¶ci± operacji procesora, pamiêæ jest wprost NIEWIARYGODNIE wolna. Zale¿a³o mi na
	szybko¶ci.</P></LI>

 <LI> Czy nie pro¶ciej zamiast tych wszystkich <code>SHL</code> zapisaæ jedno
	<code>MUL</code> lub <code>IMUL</code>?<BR>
	<P>Jasne, ¿e pro¶ciej. Przy okazji dobre kilka[na¶cie] razy wolniej.</P></LI>

 <LI>Dlaczego ci±gle <CODE>xor rej,rej</CODE>?<BR>
	<P>Szybsze ni¿ <CODE>mov rej,0</CODE>, gdzie to zero musi byæ czêsto zapisane 4 bajtami
	zerowymi. Tak wiêc i krótsze. Oprócz tego, dziêki instrukcji <code>XOR</code>
	lub <code>SUB</code> wykonanej na tym samym rejestrze, procesor wie, ¿e ten rejestr
	ju¿ jest pusty. Mo¿e to przy¶pieszyæ niektóre operacje.
	</P></LI>

 <LI>Dlaczego na niektórych etykietach s± jakie¶ znaki podkre¶lenia z przodu?<BR>
	<P>Niektóre procedury s± ¿ywcem wyjête z mojej biblioteki, pisz±c któr± musia³em zadbaæ, by
	przypadkowo nazwa jakie¶ mojej procedury nie by³a identyczna z nazw± jakiej¶ innej
	napisanej w programie korzystaj±cym z biblioteki.<BR>
	Czy nie mog³em tego potem zmieniæ?<BR>
	Jasne, ¿e mog³em. Ale nie by³o takiej potrzeby.</P></LI>

 <LI>Czemu <CODE>or rej,rej</CODE> a nie <CODE>cmp rej,0</CODE>?<BR>
	<P>OR jest krótsze i szybsze. Mo¿na te¿ u¿ywaæ <CODE>test rej,rej</CODE>, które nie zmienia
	zawarto¶ci rejestru.</P></LI>

 <LI>Czemu <CODE>or dl, &quot;0&quot;</CODE>?<BR>
	<P>Bardziej czytelne ni¿ <CODE>add/or dl,30h</CODE>. Chodzi o to, aby dodaæ kod ASCII zera. I mo¿na
	to zrobiæ bardziej lub mniej czytelnie.</P></LI>

 <LI>Co to w ogóle za instrukcja to <CODE>lea edx, [_pisz_bufor+si-1]</CODE> ?<BR>
	<P><code>LEA</code> - <span lang="en">Load Effective Address</span>:
	 do rejestru EDX wpisz adres (elementu, którego) adres
	wynosi <q>_pisz_bufor+SI-1</q>. Tak wiêc od tej pory EDX = _pisz_bufor+SI-1, czyli wskazuje
	na ostatni± cyfrê w naszym buforze. Czemu odj±³em 1? Jak widaæ w kodzie, po wpisaniu
	cyfry do bufora, zwiêkszamy SI. Gdy nasza liczba ju¿ siê skoñczy to SI pokazuje na
	nastêpne wolne miejsce po ostatniej cyfrze, a chcemy, aby pokazywa³ na ostatni±.
	St±d to minus jeden.
	</P></LI>
<!--
 <LI>Po co te ¶mieszne gwiazdki i znaki "="?<BR>
	<P>Gdy program siê rozrasta, pozwalaj± utrzymaæ czytelno¶æ. Takich linijek u¿ywam do
	oddzielania ró¿nych procedur/sekcji kodu.
	</P></LI>
-->
 <LI>Czemu FASM nie akceptuje EQU? <BR>
	<P>FASM akceptuje EQU, tylko tutaj symbol by³ zdefiniowany po u¿yciu, co najwyra¼niej
	przeszkadza FASMowi. Postawienie znaku równo¶ci zamiast EQU naprawi³o sprawê.
	</P></LI>

 <LI>Czemu jest dwukropek po etykiecie zmiennej?<BR>
	<P>Po to, aby FASM przyj±³ dyrektywê <q lang="en">times</q>. Bez dwukropka nie chcia³ skompilowaæ.
	</P></LI>

</UL>
<P>Wiem, ¿e ten program nie jest doskona³y. Ale taki ju¿ po prostu napisa³em...<BR>
Nie martwcie siê, je¶li czego¶ od razu nie zrozumiecie. Naprawdê, z czasem samo przyjdzie.
Ja te¿ przecie¿ nie umia³em wszystkiego od razu.</P>
<P>
Inny program do liczb pierwszych znajdziecie tu: <a href="asm/inni/prime.txt">prime.txt</a>.
</P>
<P>Nastêpnym razem co¶ o u³amkach i koprocesorze.</P>

<blockquote>Podstawowe prawo logiki:<BR>
 Je¿eli wiesz, ¿e nic nie wiesz, to nic nie wiesz.<BR>
  Je¿eli wiesz, ¿e nic nie wiesz, to co¶ wiesz.<BR>
  Wiêc nie wiesz, ¿e nic nie wiesz. </blockquote>

<br><br>
<div class="bezdruk">
<a accesskey="3" hreflang="pl" href="linux03.html">Poprzednia czê¶æ kursu</a> (Alt+3)<br>
<a accesskey="4" hreflang="pl" href="linux05.html">Kolejna czê¶æ kursu</a> (Alt+4)<br>
<A accesskey="1" hreflang="pl" href="../index.htm">Spis tre¶ci off-<span lang="en">line</span></a> (Alt+1)<BR>
<A accesskey="2" hreflang="pl" href="../index.php">Spis tre¶ci on-<span lang="en">line</span></a> (Alt+2)<br>
<A accesskey="0" hreflang="pl" href="../ulatwie.htm">U³atwienia dla niepe³nosprawnych</a> (Alt+0)<BR>
</div>


<BR><BR><HR>
<h2 class="nag">Æwiczenia</h2>

(mo¿na korzystaæ z zamieszczonych tu procedur)
<OL>
 <LI>Napisz program, który na ekranie wy¶wietli liczby od 90 do 100.<BR><BR></LI>
 <LI>Napisz program sprawdzaj±cy, czy dana liczba (umie¶cisz j± w kodzie, nie musi byæ
	wczytywana znik±d) jest liczb± pierwsz±.<BR><BR></LI>
 <LI>Napisz program wypisuj±cy dzielniki danej liczby (liczba te¿ w kodzie).</LI>

</OL>

</BODY></HTML>
